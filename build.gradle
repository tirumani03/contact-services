plugins {
    id "io.spring.dependency-management" version '1.0.7.RELEASE'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id "java"
    id "groovy"
    id "eclipse"
    id 'org.hidetake.swagger.generator' version '2.10.0'
    
}
repositories {
       mavenCentral()
	   jcenter() 
  }


apply plugin: "jacoco"
apply plugin: 'java' 
/*apply plugin: 'war'

war {
    archiveName = 'contact-services.war'
}*/

group 'com.ezetap.contacts.services'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

//__________________________________________________________//
//_____________________ SWAGGER CONFIGURATION ______________//
swaggerSources {
    contacts{
       def contactsYaml="${rootDir}/src/main/resources/api/contacts.yaml"
       def configJson= "${rootDir}/src/main/resources/api/contacts.json"
       inputFile= file(contactsYaml)
       def ignoreFile = "${rootDir}/src/main/resources/api/.swagger-codegen-ignore"
       code {
         language='spring'
         configFile=file(configJson)
         rawOptions=['--ignore-file-override',ignoreFile]
         components=['models','apis']
         dependsOn validation
       }
    }
}

compileJava {
    dependsOn processResources
    dependsOn swaggerSources.contacts.code
    source "${swaggerSources.contacts.code.outputDir}/src/main/java"
    
}

sourceSets {
    unitTest {
        java { srcDir 'src/test/groovy' }
        resources { srcDir 'src/test/resources' }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
    main {
        java { srcDirs += "${swaggerSources.contacts.code.outputDir}/src/main/java" }
    }
}

//__________________________________________________________//
//_____________ TEST CONFIG SETTINGS AREA __________________//
test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
    testLogging { // Show that tests are run in the command-line output
        events 'passed' // 'started' is also an option
    }
}

//Ensure that the HTML reports of unit and integration tests are written to different directories.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

//__________________________________________________________//
//___________________ JACOCO PLUGIN CONFIG AREA ____________//
jacoco { toolVersion = "0.8.2" }

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
        fileTree(dir: it,
            exclude: ['**/model/**'])
        })
    }
}

configurations{
    runtime.extendsFrom providedRuntime
    swaggerCodegen.extendsFrom annotationProcessor
}
dependencies{
    compile 'io.swagger:swagger-annotations:1.5.18'
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.3.1'
    
    // https://mvnrepository.com/artifact/io.springfox/springfox-swagger-ui
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile ('org.springframework.boot:spring-boot-starter-data-jpa')
 	compile ('org.springframework.boot:spring-boot-starter-security')
	compile ('org.springframework.boot:spring-boot-starter-web')
	
	compile 'org.springframework.boot:spring-boot-devtools'
	// In Memory DB H2
	compile 'com.h2database:h2'
	
	// HATEOS
	compile ('org.springframework.boot:spring-boot-starter-hateoas')
	
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.security:spring-security-test'
    testCompile(group: 'org.spockframework', name: 'spock-core', version:'1.1-groovy-2.4') { exclude(module: 'groovy-all') }
    testCompile(
                'junit:junit:4.12',
                'org.codehaus.groovy:groovy-all:2.4.10',
                'info.cukes:cucumber-java:1.2.5',
                'info.cukes:cucumber-junit:1.2.5',
                'org.glassfish.jersey.media:jersey-media-json-jackson:2.14',
                'org.glassfish:javax.json:1.0.4',
                'javax.json:javax.json-api:1.0'
                )
    repositories {
       mavenCentral()
	   jcenter() 
    }
}